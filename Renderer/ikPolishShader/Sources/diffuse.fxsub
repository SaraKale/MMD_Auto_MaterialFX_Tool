// ŠgŽU”½ŽË€‚ÌŒvŽZ


#define NEAR_DISTANCE	1.0		// ‹ß‚­‚É‚ ‚é•¨‚Ì‰e‚ð–³Ž‹‚·‚é‹——£

float3 GetIndirectlight(float2 texCoord, float3 N, MaterialParam mat, float4 albedo)
{
	float ao = GetSSAO(texCoord);
	float3 envColor = GetEnvColor(N, 1.0).rgb;

	#if ENABLE_SSGI > 0
	float4 ssgi = tex2D(FullWorkSamp, texCoord);
	ao = min(ao, ssgi.w);
	float3 selfReflection = GTAOMultiBounce(ao, albedo.rgb);
	float3 ambient = ssgi.rgb + envColor * selfReflection;
	#else
	float3 selfReflection = GTAOMultiBounce(ao, albedo.rgb);
	float3 ambient = envColor * selfReflection;
	#endif

	return ambient.rgb * AmbientScale;
}

// ‹ß‚¢ƒVƒƒƒhƒEƒLƒƒƒXƒ^[‚©‚ç‚Ì‰e‚ð–³Ž‹‚·‚é
float TweakTranslucentShadow(float shadow, float shadowDepth)
{
	float att = SAFE_EXP2(-(shadowDepth + 0.01) * (4.0 * LOG2_E / NEAR_DISTANCE));
	return lerp(shadow, 1, att);
}

float3 CalcTranslucentColor(float3 L, float3 N, MaterialParam mat, float3 albedo, float2 shadowInfo)
{
	float shadow = shadowInfo.x;
	float shadowDepth = shadowInfo.y;
	float isTranslucence = (mat.attribute == MT_FACE || mat.attribute == MT_LEAF);
	float translucencyShadow = TweakTranslucentShadow(shadow, shadowDepth);
	shadow = isTranslucence ? translucencyShadow : shadow;

	float lambert = dot(N, L);
	float halfLambert = saturate(lambert * 0.5 + 0.5);

	shadow = min(halfLambert, shadow * 0.5 + 0.5);
	float3 shadowColor = lerp(albedo * albedo * 0.5, albedo, halfLambert);
	// float3 shadowColor = pow(albedo, 2.0 - halfLambert) * (halfLambert * 0.5 + 0.5);
	float3 col = lerp(shadowColor, 1, shadow) * 0.95;

	return SAFE_EXP2((col - 1) * (LOG2_E * ThicknessScale)) / PI;
}

float4 PS_CalcDiffuse( float4 Tex: TEXCOORD0 ) : COLOR
{
	float2 texCoord = Tex.xy;

	float4 albedo = tex2D(ColorMap, texCoord);
	MaterialParam mat = GetMaterial(texCoord, albedo);
	GeometryInfo geom = GetWND(texCoord);
	float2 shadowInfo = tex2D(ShadowmapSamp, texCoord).xy;

	float3 V = normalize(CameraPosition - geom.wpos);
	float3 L = -LightDirection;
	float3 N = geom.nd.normal;

	float3 diffuse = CalcDiffuse(L, N, V, mat) * shadowInfo.x;
	float3 scatter = CalcTranslucentColor(L, N, mat, albedo.rgb, shadowInfo);
	scatter = max(scatter, diffuse);
	float3 directlight = lerp(diffuse, scatter, mat.sssValue * 0.5) * LightColor;
	directlight += GetExtraDiffuseLight(texCoord).rgb * LightScale / PI;
	float3 indirectlight = GetIndirectlight(texCoord, N, mat, albedo);

	float3 light = directlight + indirectlight;
	light *= mat.cavity;

	#if SSSBlurCount > 0
	// float result_a = mat.sssValue;
	float result_a = (mat.sssValue > 0) ? mat.sssValue : shadowInfo.x;
	#else
	float result_a = shadowInfo.x;
	#endif

	light = ApplyFog(light, -V, geom.nd.depth);
	light *= saturate(1.0 - mat.metalness);

	return float4(light, result_a);
}
